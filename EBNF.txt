
program -> declaration program 
        -> definition program  
        -> main

declaration -> INT ID 
            -> INT assignment 
            -> UINT ID 
            -> UINT assignment 
            -> CHAR ID 
            -> CHAR assignment
            -> STRING [ INT_N ] ID 
            -> STRING [ INT_N ] ID ARROW SCAN
            -> STRING [ INT_N ] ID ARROW STR
            -> STRING [ ] ID ARROW STR
            -> FLOAT ID 
            -> FLOAT assignment

assignment  -> ID ARROW nexp 
            -> ID ARROW STR 
            -> ID ARROW SCAN

nexp    -> INT_N 
        -> FLO_N 
        -> ID 
        -> call 
        -> - nexp nexp 
        -> + nexp nexp
        -> * nexp nexp 
        -> / nexp nexp 
        -> % nexp nexp 

increment   -> + + ID 
decrement   -> - - ID

bexp    -> TRUE 
        -> FALSE 
        -> call 
        -> = bexp bexp 
        -> & bexp bexp 
        -> | bexp bexp
        -> = nexp nexp 
        -> < nexp nexp 
        -> > nexp nexp 
        -> < = nexp nexp 
        -> > = nexp nexp 
        -> ! bexp

definition  -> signature ARROW arguments { code }
signature   -> FUNC ID : INT 
            -> FUNC ID : UINT
            -> FUNC ID : CHAR 
            -> FUNC ID : STRING
            -> FUNC ID : FLOAT 
            -> FUNC ID : BOOL 
            -> FUNC ID : VOID

main    -> MAIN : INT { code }
        -> MAIN : VOID { code }

arguments   -> EPSILON 
            -> argument arguments

argument    -> INT : ID 
            -> UINT : ID 
            -> CHAR : ID
            -> STRING : ID 
            -> FLOAT : ID 

code    -> declaration code 
        -> assignment code 
        -> increment code
        -> decrement code
        -> WHILE bexp { code } code
        -> IF bexp { code } ELSE { code } code
        -> IF bexp  { code } code
        -> call code
        -> PRINT ( parameters ) code
        -> RET parameter code
        -> RET code
        -> EXIT 
        -> EPSILON

call        -> ID ( parameters )
parameters  -> EPSILON 
            -> parameter parameters
parameter   -> nexp 
            -> bexp 
            -> STR

